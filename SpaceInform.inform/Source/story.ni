"Xenocide" by William Pacini and Brian Gabin

Chapter 0 - Inform Setup

Include Conversation Package by Eric Eve.
The story headline is "A space horror".
Use American dialect.
Use no scoring.

Chapter 1 - Building the Map

Rooms can be infested or safe.
Rooms can be supported or not supported. 
A room is usually safe and supported.

Port is a direction. The opposite of port is starboard. Understand "p" as port.
Starboard is a direction. The opposite of starboard is port. Understand "sb" as starboard.
Fore is a direction. The opposite of fore is aft. Understand "f" as fore.
Aft is a direction. The opposite of aft is fore. Understand "af" as aft.

Instead of going South : say "Cardinal directions don't make sense on a spaceship".
Instead of going North : say "Cardinal directions don't make sense on a spaceship".
Instead of going East : say "Cardinal directions don't make sense on a spaceship".
Instead of going West : say "Cardinal directions don't make sense on a spaceship".
Instead of going Southwest : say "Cardinal directions don't make sense on a spaceship".
Instead of going Northwest : say "Cardinal directions don't make sense on a spaceship".
Instead of going Southeast : say "Cardinal directions don't make sense on a spaceship".
Instead of going Northeast : say "Cardinal directions don't make sense on a spaceship".

Instead of going fore in Mining Vessel : say "Remember what you're here for. The answer lies toward the Aft of the ship.".
Instead of going port in Mining Vessel : say "Remember what you're here for. The answer lies toward the Aft of the ship.".
Instead of going starboard in Mining Vessel : say "Remember what you're here for. The answer lies toward the Aft of the ship.".
 
The Mining Vessel is a room. The description is "It's a small two seat mining ship that you've lovingly named 'Ol' Faithful' after years of service. The Space Hulk that the distress signal came from sits [bold type]AFT[roman type] of the little vessel. If things go poorly, it may be your only means of escape.".

The Airlock is a door. It is aft of the Mining Vessel. It is fore of the Entry Commons. it is unlocked. The description is "A small bisected tube with a decompression chamber in the middle and large air tight blast doors seperateing the sections. You've seen hundreds of these before and can move right through without much hassle.".

The Entry Commons is a room. It is aft of the Airlock. The description is "The Entry commons is the first room anyone enters upon boarding the ship. Clean as ever, you would never have known that anything was amiss standing in the Entry Commons if not for the lack of people. The ship's census placard is hanging on the wall. [line break][line break][bold type][roman type]".

The Hallway is a room. It is port of the Entry Commons. The description is "A large passage  running along the port exterior of the ship. The monitors that line the outer wall simulate windows, and outside you can see the deserted asteroid field that you've been mining your whole life pass by. A line of small trees run along the monitors. [line break] [line break] There are three exits out of the Hallway towards the [bold type]AFT[roman type] of the ship you see a sign labeled Barracks. Towards the  [bold type]FORE[roman type] of the ship the sign reads entry commons. and about halfway down on the starboard you see a sign reading elevator, but where the elevator should be, there is a rather large pile of rubble.".

The Exercise Room is a room. It is starboard of the Entry Commons. The description is "[first time]Men in space need more exercise than those planet side. The lower gravity means that trivial tasks like walking to breakfast don't have as much of a strain on your body, and the Galactic marines need to be fighting fit where ever they land. You think that last thought may have come from a recruiting video you saw once. On the floor you see a piece of paper.[line break][line break][only] A row of stationary bikes run along the walls and some exercise machines are in the center for non aerobic workouts. The room has the usual sweaty aroma of a gym.  The passageway back to the  Entry Hall lies to the [bold type]PORT[roman type]".

The Barracks is a room. It is aft of the Hallway. The description is "[first time]As you exit the hallway, a foul stench gets caught in your nose, reeking of waste and decay. After a moment's hesitation you remember that the way behind is blocked. You gather your courage and taking a deep breath enter the barracks. [only][line break][line break]Its clear that a Line of bunks ran down the  fore and aft side walls, but now they and the tatters that once were the sheets are strewn about the room.  Broken shards of furniture are smashed up against the walls and piled in the corners. But worst of all is the bodies. Your rough count is about fifteen dead men, but the limbs make it hard to be sure.".

The Galley is an infested room. It is starboard of the Elevator. The description is "The room has been torn to pieces. This is where the creatures had been quarantined in their bulk after the accident. The tables and chairs have been ripped up and replaced with a floor covered by half devoured bodies and feces. One of the [italic type]aliens[roman type] seems a bit odd and is sprawled out in the center of the room on the last remaining table of the Dining Hall.".

The Medical Bay is a room.  The description is "The room is very clean. The aliens have clearly never been inside of here. Along one wall the doctor has his standard medical tools and medicine cabinet".

The Crew's Quarters is a room. It is starboard of the Barracks. The description is "[first time]The Crew's Quarter's seems to be a bit of a sty, but is nothing compared to the massacre you just came from. There are no body parts anywhere, just a some ruffled up papers and unmade beds.[only][line break][line break] The furniture is realtively intact with four bunks, one on each wall accompanied by a desk and a footlocker. The Engineer's footlocker seems to be the only one you can get into.".

The Officer's Quarters is a room. It is aft of the Barracks. The description is "The Officer's Quarters is in disarray. The  furniture has alll been shoved into the corner revealing the emergency ladder  that goes up and down. There are signs of struggle in hear and a bit a blood, ".

The Engine Room is a room. It is down of the Officer's Quarters. The description is "[first time]You emerge from the ladder's narrow passage into the Engine Room.[line break][line break][only]Boilers, pipes, and gauges crisscross the Engine Room. In the back sits the massive Power Generator. On the front of the Power Generator a console reads the error code...".

The Armory is a room. It is fore of the Engine Room. The description is "Racks of Aussault Rifles fill the locked safe and crates upon crates of ammunition are stacked up against the walls. [one of] Too bad it was all down here.[or] Some good it did them.[or] ts a damn shame they weren't expecting to use any before they got planetside.[in random order]".

The Life Support Systems is a room. It is fore of the armory. The description is "The room is remarkably dry. Almost mimicking a clean room. A series of dull pipes run between a few pumps. The pipes are labeled with the names of rooms they are going to. In the center is a console.".

The Brig is a room. The description is "By far the most foul smelling room in the entire ship. Along the edges are four cells numbered accordingly and at the end a single desk. Inside the cells is a puddle of fluids of all sorts of colors you would never image. The fourth cell contains a man with a hole eaten out where his face should have been. You can make out some scribbles in one of the cells. It reads, '[one of]Dead men tell no tales[or]They got me down here on quarantine, Doc says Ill be good to go soon[or]For a good time call...[in random order].'".

The Weapons Systems Room is a room. It is up of the Officer's Quarters. The description is "The exterior weapons systems are controlled from in this room. The manual override stations look dusty and unused. All the weapons systems in the world don't do a lick of good if your guns can't point the right way. A rack of Dominion Astral Torpedoes sit in the corner ready to be loaded into the pipes. In the corner sits the nuclear warhead, saved for the biggest of targets."

The Observation Deck is a room. It is fore of the Weapons Systems. The description is "In the center of the Observation Deck you can see down through a safety glass panel into the center of the Galley.  Most of the viewing port has been covered in the phlegmish substance, but through the direct center you can see about ten square feet of the Galley floor.The furniture seems to be haphazardly spread out and the carnage similar to the Barracks. The top floor itself seems to have been untouched by the incident."

 The Bridge is an infested room. The description is "At the center of the bridge is a single rotating chair sitting behind a console. You can see the imprint of the Captain's rear end pressed into the velvet. A series of monitors display the ship's surroundings, meant to mimic the windows of yore.".
 
The Captain's Quarters is a room. It is starboard of the Observation Deck. The description is "The Captain's quarters is perhaps the only single on the ship. It's cozy yet comfortable. The bed has been made and a stack of papers sit on his desk.".

The Hallway is port of the Barracks.
The Entry Commons is fore of the Hallway.

The Galley's Aft  Blastdoor is a door. It is aft of the Galley. It is fore of the Barracks. It is closed. It is locked. The description is "The sign above the Blastdoor reads 'Galley,' but the window has been obscured by some sort of phlegmish ooze. There is a faint scratching sound at the door.".
The Galley's  Fore Blastdoor is a door. It is fore of the Galley. It is aft of the Entry Commons. It is closed. It is locked. The description is "[first time]The port hole window is fogged up, but when you rub it off, you can faintly make out a bit of movement in the background. Just as you lean in, a tentacle moves up over the window and the other side begins to glaze over.[only][line break][line break]The window is completely blocked on the other side , but it reads 'Galley' on the top.".
The Galley's Starboard Blastdoor is a door. It is starboard of the Galley. It is port of the Medical Bay. It is closed. It is locked. The description is "The blastdoor is heavily reinforced. The viewing port has been covered with gauss pads".

The Bridge's Blastdoor is a door. It is aft of the Bridge. It is fore of the Observation Deck. It is closed. It is locked. The description is "There are no windows on this Blastdoor, but just a sign that says, 'Bridge.'".

The Brig's Blastdoor is a door. It is starboard of the Armory. It is port of the Brig.  It is closed. It is locked.The description is "A large yellow and black striped Blastdoor with a sign that say Brig at the top.".

Chapter 2 - Elevator

The Elevator is a room. It is starboard of the Hallway. It is port of the Observation Deck. The description is "Elevator description".
Instead of going port in the Armory:
	If the Power Console is broken, say "the power is off.";
	If the Power Console is not broken
	begin;
		If Armory is not mapped starboard of the Elevator
		begin;
			now Armory is mapped starboard of the Elevator;
			now Elevator is mapped port of the Armory;
			now nothing is mapped port of the Elevator;
			say "You press the button to call the elevator and stand anxiously in the Armory listening to sounds of the creatures moving throughout the ship.";
		end if;
		continue the action;
	end if;
Instead of going starboard in the Hallway:
	If the Power Console is broken, say "the power is off.";
	If the Power Console is not broken
	begin;
		If Galley is not mapped starboard of the Elevator
		begin;
			now Galley is mapped starboard of the Elevator;
			now Hallway is mapped port of the Elevator;
			say "You press the button to call the elevator.";
		end if;
		continue the action;
	end if;
Instead of going port in the Galley:
	If the Power Console is broken, say "the power is off.";
	If the Power Console is not broken
	begin;
		If Galley is not mapped starboard of the Elevator
		begin;
			now Galley is mapped starboard of the Elevator;
			now Hallway is mapped port of the Elevator;
			say "You press the button to call the elevator.";
		end if;
		continue the action;
	end if;

Instead of going port in the Observation Deck:
	If the Power Console is broken, say "the power is off.";
	If the Power Console is not broken
	begin;
		If Observation Deck is not mapped starboard of the Elevator
		begin;
			now Observation Deck is mapped starboard of the Elevator;
			now nothing is mapped port of the Elevator;
			say "You press the button to call the elevator.";
		end if;
		continue the action;
	end if;
Instead of going up in the Elevator:
	If the Power Console is broken, say "the power is off.";
	If the Power Console is not broken
	begin;
		If Observation Deck is mapped starboard of the Elevator, say "The elevator is already on the top level.";
		If Galley is mapped starboard of the Elevator
		begin;
			now Observation Deck is mapped starboard of the Elevator;
			now nothing is mapped port of the Elevator;
			say "The door closes and the elevator churns upwards to the top level.";
		end if;
		If Armory is mapped starboard of the Elevator
		begin;
			now Galley is mapped starboard of the Elevator;
			now Hallway is mapped port of the Elevator;
			say "The door closes and the elevator churns upwards to the main level.";
		end if;
	end if;
Instead of going down in the Elevator:
	If the Power Console is broken, say "the power is off.";
	If the Power Console is not broken
	begin;
		If Armory is mapped starboard of the Elevator, say "The elevator is already on the bottom level.";
		If Galley is mapped starboard of the Elevator
		begin;
			now Armory is mapped starboard of the Elevator;
			now nothing is mapped port of the Elevator;
			say "The door closes and the elevator churns upwards to the bottom level.";
		end if;
		If Observation Deck is mapped starboard of the Elevator
		begin;
			now Galley is mapped starboard of the Elevator;
			now Hallway is mapped port of the Elevator;
			say "The door closes and the elevator churns upwards to the main level.";
		end if;
	end if;

Instead of going aft in the Elevator, say "The console starts glowing red and says, 'I can't let you do that, Dave!' [line break][line break] Must be an old relic in the system you think to yourself."
Instead of going fore in the Elevator, say "The console starts glowing red and says, 'I can't let you do that ,Dave!' [line break][line break] Must be an old relic in the system you think to yourself."

Chapter 3 - Things and Scenery

The doctor's desk is a supporter. It is in the brig. The description is "A plain desk with no drawers.".

The medical cabinet is scenery in the medical Bay. the description is "Nothing remarkable, you see some various drugs and prescriptions.".

The medical tools is scenery in the medical Bay. the description is "A random collection of tools, including a scalpel, a laser wound sealer, a stethoscope., ".

Some Trees are scenery in the Hallway. The description is "While providing no practical use, the trees give an aesthetic comfort to the crew members and soldiers that helps stave off the dreaded Cabin Fever."

The alien is a supporter. It is fixed in place. It is in the Galley.The description is "The alien has four major appendages coming out of its torso and seemed to have been crawling along on all four when it finally collapsed. It's got a greenish hue and clammy skin. Upon closer inspection, you notice that a series of tentacles are coming out of the creature's throat and creeping around the head and down its back, leaving a sort of carapace in its wake. Near the bottom, however the skin remains softer and more muscular."

Some bunks are scenery. They are undescribed. They are in the Crew's Quarters.The description is "The bunks are still in place along the walls. They are unmade and were seemingly left in a hurry."

A pair of dog tags are an object. They are on the alien. The description is "A set of Dominion dog tags, reading the name Seth Wormund.".

Bodies are a backdrop. They are in the barracks. They are in the galley. They are in the officer's quarters. They are in the Armory. They are in the brig. The description is "dead bodies everywhere".

Stationary Bikes are scenery. They are in the Exercise Room. "The bikes are bolted to the ground to avoid damage during ship maneuvers. They were FitCo brand, normal government issue crap, but they probably got the job done."

Some Exercise Machines are a supporter. They are fixed in place. The description is "Various machines to work out the different muscles were in the center of the room. All the machines seemed to be dry as a bone.".

Ship Census is scenery. it is in the Entry Commons. The description is "The Census Placard reads: [line break][line break]Ship Name: The Dominion's Hammer[line break]Ship Type: Evictor Class Military Transport [line break]Sister Ship: The Dominion's Anvil [line break]Crew Size: Six [line break]Personnel Capacity: Fifty Galactic Marines[line break]Emergency Frequency: 6232".

Engineer's Footlocker is a container. It is undescribed. It is in the Crew's Quarters. It is closed. It is openable. It is fixed in place.The description is " A standard footlocker located at the bottom of his bunk. All the man's worldly possessions would have fit inside this roughly four square foot box. It appears to be bolted to the ground to avoid mid flight shuffling. Inscribed on top it reads, 'Mason Wrightfield - First Engineer'". Understand "locker" as footlocker.

Engineer's Desk is a supporter. It is undescribed. It is in the Crew's Quarters. The description is "A crudely slapped together desk, sitting next to the Engineer's bunk. These affordances were only given to people who truly needed them when space is so limited."

A manual is a kind of thing.
A manual called Engineer's Manual is on the Engineer's desk. The description is "[first time] The Manual is sitting spread open on the desk with torn pages strewn about. Clearly someone must have been in a hurry. You tuck the pages back in as you see fit and close the book properly. [only]The cover reads 'Evictor Class Military Transport Maintenance Manual' in big golden letters across the cover. Inside it seems to contain schematics for each of the ships integral systems. Sadly, the book seems to be worn and torn , clearly heavily used and not well taken care of.".

The gun safe is a container. It is in the armory. It is fixed in place. It is closed. It is locked. The description is "The safe is completely sealed, and besides whats a poor asteroid miner like yourself going to do with a Dominion Issued Assault Rifle. Probably just end up shooting yourself.".

The Captain's Desk is a supporter. It is in the Captain's Quarters. The description is "An nice large desk. The drawers are locked.".

The stapler is a thing. It is on the Captain's desk.  The description is "An antique swingline stapler.".

There is a Multi-tool in the Engineer's Footlocker. The description is "This multipurpose engineering tool has nearly infinite uses. In the right hands it can fix almost any machine, conversely in the wrong hands it can permanently break anything." Understand "tool" as multi-tool.

Chapter 4 - Journals

Engineer's Journal Entry is a thing. It is in the Engineer's footlocker. The description is "All of these infernal Marines climbing over my ship, making a mess of the place. And to top it all off the Power Generator is on the fritz. What a week.".

Doctor's Journal Entry is a thing. It is on the Doctor's Desk. The description is "It reads, [line break][line break]The tests have been a complete success. The hosts are taking the alien seed splendidly. A few more Marines with the sniffles and I'll be able to show my findings to the Council. The next wave of super soldiers will be the best mankind has ever seen. Exterminatus here we come.".

Captain's Journal Entry is a thing. It is on the Captain's desk. The description is "It reads, [line break][line break]I hate myself...When a captain cannot control what goes on beneath his desk, it maybe time to hang it all up. I recently found out what the Doctor has been doing under my nose this whole time, but its too late to stop him. This is no time for a mutiny, we are too far out. Hopefully I can be forgiven for going along with this ruse until we make port next week.".

Pvt Johansen's Journal Entry is a thing. It is in the Exercise Room. The description is "[first time]You dry off the sweat stains.[line break][line break][only]It reads, [line break][line break]They came from below. The bastards started flowing from the safety  ladders.  Too bad out guns are down below. Its only a matter of time now.".

Pvt Wormund Journal Entry is a thing. It is in the Barracks. The description is "[first time]The paper is covered in blood and ooze, but you manage to make out what was written there before.[line break][line break][only]It reads, [line break][line break]All these extra workouts are really time consuming, but then again it's not like there's anything else to bloody do around here. Thats what I get for signing up to be a Dominion grunt I suppose. Been a bit queasy lately, the Captain says I'll get used to it and I should quit my complaining. However the Doctor recommends that I start seeing him, just to be sure it's not something serious. Getting on the Doc's good side can't be a bad thing, best case it's nothing and he gives me some nice supplements to make  me bigger, faster, and stronger. Heh, I guess government work isn't all bad.".

Sgt  Castle's Journal Entry is a thing. It is in the Officer's Quarters. The description is "[first time]You flatten out the paper.[line break][line break][only]It reads, [line break][line break]A terrible virus seems to be spreading around the ship lately. perhaps that is why I've felt so light headed. Maybe I' should go see the Doc. A few of the men have it so bad that the Doctor recommends we begin quarantining them down in the brig. He's set up a desk down there to do observations and claims his hazmat suit will keep him uninfected. Hopefully the boys will be ok when we land for resupplies in a few weeks. Dinner's ready so I best head over there before the food is all gone. The Galley is such a pleasant room.".


Chapter 5 - Scripted Events

When play begins:
say "Puttering along on your way to work at the asteroid mines, you recieve a distress signal.[line break]Being the brave heroic soul that you are, you set course for the signal. As  you come across the space hulk floating through the asteroid field  the airlock beckons and you connect.".

After going aft in the Entry Commons: say "As you pass though the airlock into the Entry Commons, you hear a loud 'Slam!' from behind. Wheeling on the spot, you realize to your horrors that the blast doors have sealed you in, for seemingly no reason. After about fifteen seconds, you hear a man's voice. [line break][line break] 'Hello there, do not be alarmed.'[line break][line break] The  sound is coming from an intercom in the corner of the room. [line break][line break] 'I've only blocked off your passage because I'm a desperate man, who's been cooped up in this floating prison for far too long. I promise that if you can get me out I'll let you go. I'm directly above you in the Bridge but in order to get here, you'll need to restore the power to certain parts of the ship and get the elevator working. I'll try to keep you updated as best as possible, but communication is a bit spotty. Time is of the essence, I'll be waiting.' [line break][line break] With that the intercom cuts off and the room returns to an eerie silence."; now The Airlock is locked; now The Airlock is closed.

After going aft in the Weapons Systems Room, say "You take this moment to unlock the emergency ladder hatch in the back so that you quickly and freely travel between the levels."

Instead of going up from Officer's Quarters:
	if Weapons Systems Room is unvisited:
		say "You climb the ladder and try to open the hatch to no avail. It seems these hatches are locked from the top. If you make it up there later you should be able to unlock it and use the ladder instead of the elevator.";
	otherwise:
		continue the action.

Instead of going down from Officer's Quarters:
	if Engine Room is unvisited, say "You examine the hatch below the emergency ladder and find that it can be unlocked from this side. After unlocking it you descend below.";
	continue the action.

After going:
	if the location is not supported :
		do suffocation ending;
	otherwise:
		continue the action.

After opening a door:
	if the other side of the noun from the location is infested:
		do eaten ending;
	otherwise:
		continue the action.

To do suffocation ending:
	say "suffocation ending.";
	end the story.

To do eaten ending:
	say "eaten ending.";
	end the story.
	
To do destruction ending:
	say "destruction ending.";
	end the story.
	
To do captain ending:
	say "captain ending.";
	end the story.
	
To do doctor ending:
	say "doctor ending.";
	end the story.
	
To do escape ending:
	say "escape ending.";
	end the story.

Chapter 6 - Conversations

The captain-introduction-node is a convnode.
node-introduction for captain-introduction-node:
	say "'There you are! Took you long enough to get here.', the Captain exclaims ungratefully.";
	say "In disbelief you say, 'Give me one good reason why I shouldn't just leave your sorry ass on this Ship and leave.'";
	say "'As much as I'd like to say I know everything that happens on this ships I command there are somethings you can't control. The goings on below decks were directed by my superiors and completely unknown to me. Had I any idea how much these top secret dealings were endangering my crew I wouldn't have allowed them to happen.";
	say "Sighing you then say, 'Fine."

what happened is a subject.
experiments is an unfamiliar subject.
power generator is a subject.
life support system is a subject.
aliens is a subject.

The doctor-firstcontact-node is a convnode. The next-node is doctor-midcontact-node.
The ask-suggestions are { self-suggestion, what happened }.
After saying hello to the Doctor when at-node doctor-firstcontact-node:
	say "'Hello? Is anyone on this frequency?', you ask.";
	say "After a few silent seconds a man responds ecstatically, 'Hello!? Who's this? Are you a survivor or a rescuer?'";
	say "'I'm a miner who responded to this ship's distress beacon and then got trapped on here by your coward captain.' you explain.";
	say "'I'm not surprised. I'm the ship's medical professional. I've been trapped in the med bay ever since those things attacked.' said the Doctor.";

The doctor-midcontact-node is a convnode. The next-node is a doctor-midcontact-node.
The ask-suggestions are { self-suggestion, Captain, what happened, aliens, power generator }.
After saying hello to the Doctor when at-node doctor-midcontact-node:
	say "'Doctor, are you still there?'";
	say "'Yes, yes. I'm still trapped in the metal death box right next to a room full of deadly creatures. What do want?"
Response for doctor-midcontact-node when asked about life support system:
	say "'There's a Life Support System, do you think messing with that would help at all?', you ask.";
	say "'If you can figure out how to operate the Life Support Console you may be able to cut oxygen off to certain parts of the ship. If were lucky these creatures need to breathe just like us.'"
Response for doctor-midcontact-node when asked about power generator and the power console is broken:
	say "'I need to repair the ship's power generator to access the rest of the ship but I don't have experience with this kinda stuff. Where do I even begin?'";
	say "'Hmm.', you can hear the Doctor thinking out loud through the radio.";
	say "'You might be able to find the engineers possessions in their quarters. They're bound to have some technical documentation there."
Response for doctor-midcontact-node when asked about the brig and experiments are unknown:
	say "'When I went down below I couldn't get access to the brig. Do you know anything about what's in there?'";
	say "'The brig was where the attacks started. As a medical professional I strongly advise you don't go in there, it'd be bad for your health."
Response for doctor-midcontact-node when asked about Captain:
	say "'What can you tell me about the ship's Captain?'";
	say "'Our illustrious Captain is god-damned coward. Almost immediately after the attacks began he sealed off the Galley locking most of the ship's crew in there securing their demise. Then when you arrive he traps you aboard this death trap just to save his own sorry ass. In my opinion, a man like that doesn't deserve the kindness of being rescued."
Response for doctor-midcontact-node when asked about aliens:
	say "'What can you tell me about the aliens?', you inquire.";
	say "Taking a deep breath the Doctor explains, 'I got locked in the medical bay before just as they began to make their way into the Galley so I didn't get to see much of them. I can tell you that those bastards have utterly destroyed the Galley and killed everyone they've run into. I certain they know I'm in here. They spent hours attacking the blastdoors before giving up, it may only be a matter of time before they try and again and make it in.'";
Response for doctor-midcontact-node when asked about Doctor:
	say "You ask the Doctor, 'Tell me more about yourself. What were your responsibilities aboard this vessel?'";
	say "'As I said I'm the ship's active medical professional. I was tasked with keeping the crew of this ship healthy before all hell broke loose.', the Doctor explained."
Response for doctor-midcontact-node when asked about what happened:
	say "'I've seen some seriously messed up shit on here. What the hell happened here?', you ask.";
	say "'It happened so quickly. I had just come up here to gather some medical charts when the screaming began below. Most of the crew was having the lunch being served in the Galley when the aliens began pouring in the from the lower level. The Captain initiated the lock-down on the Galley locking nearly all of the crew in with those monsters. I've been trapped back in the med bay since."
Response for doctor-midcontact-node when asked about experiments:
	say "'Doctor, I explored the brig and it wasn't pretty down there. There was a lot of medical equipment down there and a lot of dead bodies. You being the ship's head medical professional I can only assume you know something about what was going on down there.'";
	say "The Doctor remained silent on the other end of the radio for quite some time before speaking up, 'We had been conducting medical tests on a group of prisoners we were transporting. A number of the prisoners were exhibiting abnormal health conditions. One of the prisoners must of been a carrier for this damned virus.'";
	say "The Doctors tone has clearly changed, it's pretty obvious that the good Doctor knows a lot more than he is letting on about what happened on this ship[node doctor-confrontational-node]." 
Before saying goodbye to the Doctor when at-node doctor-midcontact-node:
	say "'I'll stay in contact with you Doctor. I'm going to get back to it.'";
	say "'Good luck son.'";
	
The doctor-confrontational-node is a convnode. The next-node is doctor-confrontational-node.
After saying hello to the Doctor when at-node doctor-confrontational-node:
	say "'Doctor.'";
	say "What is it boy.', the Doctor snaps."
Before saying goodbye to the Doctor when at-node doctor-confrontational-node:
	say "'Over and out, Doctor.'";
	say "'..Over and out.'"

Chapter 7 - Radio Communication

A radio is a kind of thing.
The player is holding a radio called Short-range Radio. The Description of Short-range Radio is "A simple Short-range radio for emergencies. You won't be able to contact anyone with it unless you can find out which frequency they might be on.[line break](Use radio with: radio (frequency) with (radio), and disconnect)[line break]".
A radio called the Captain's Radio is in the Bridge. Understand "5421" as the Captain's radio.
A radio called the Doctor's Radio is in the Medical Bay. Understand "6232" as the Doctor's Radio.

Understand "radio [any radio] on [something]" as contacting it on.
Understand "radio [any radio]" as contacting it on.
Understand the commands "contact" or "call" as "radio".

Connection relates one thing to another (called the other party).
The verb to reach (it reaches, they reach, it reached) implies the connection relation.

Contacting it on is an action applying to one visible thing and one thing.

Check contacting it on:
	if the second noun is not an radio, say "[The second noun] is unlikely to be much use in that respect." instead;
	if the second noun is the noun, say "You get a busy signal." instead.
	
Carry out contacting it on:
	if a person (called the listener) can see the noun, now the player reaches the listener.
	
Report contacting it on:
	say "You successfully connect with someone on the other end of the radio."
	
Rule for supplying a missing second noun while contacting something on:
	assign a radio.
	
To assign a radio:
	if the player can touch a radio (called the current phone):
		say "(on [the current phone])[line break]";
		now the second noun is the current phone;
	otherwise:
		say "You don't have a radio handy.";
		
Before contacting something on something when the player reaches someone:
	say "(first ending your conversation with [the other party of the player])[command clarification break]";
	end current conversation.
	
Understand "disconnect [something]" as disconnecting.

Disconnecting is an action applying to one thing.

Check disconnecting:
	if the noun is not a radio, say "You can't disconnect [the noun]." instead;
	if the player does not reach someone, say "You're not communicating with anyone." instead.
	
Carry out disconnecting:
	now the player does not reach anyone;
	
	
Report disconnecting:
	say "You turn your radio off."
	
Before going somewhere when the player reaches someone:
	say "(first ending your conversation with [the other party of the player])[command clarification break]";
	end current conversation.
	
Understand "radio [text]" as misdialing. Misdialing is an action applying to one topic. Carry out misdialing: say "There's nothing but static on the radio."

Before misdialing when the player reaches someone:
	say "(first ending your conversation with [the other party of the player])[command clarification break]";
	end current conversation.
	
To end current conversation:
	let the current radio be a random radio which can be touched by the player;
	silently try disconnecting the current radio.
	
After deciding the scope of the player while the player reaches someone:
	place the other party of the player in scope, but not its contents.
	
To decide whether acting through the line:
	if the noun is something and the location of the noun is not the location of the player and the noun is not a door:
		yes;
	if the second noun is something and the location of the second noun is not the location of the player and the noun is not a door:
		yes;
	no.
	
Visibility rule when acting through the line:
	there is insufficient light.
	
Rule for printing a refusal to act in the dark when acting through the line:
	say "You're not on a video phone, so you can only hear." instead.
	
A rule for reaching inside a room (called destination):
	if the other party of the player is enclosed by the destination:
		say "Though it may sound like [the other party of the player] is standing right next to you, he is not.";
		deny access.

Chapter 8 - Puzzles

A console is a kind of thing.
A console is fixed in place.
A console can be either broken or not broken.
A console can be either understandable or confusing.
There is a console called Life Support Console in Life Support Systems. The Life Support Console is confusing and not broken.
There is a console called Power Console in the Engine Room. The Power Console is broken and confusing.

Understand "lookup error code [a number] in [something]" as finding code in.
Understand "lookup error code [a number]" as finding code in.
Understand "lookup [a number] in [something]" as finding code in.
Understand "lookup [a number]" as finding code in.

Understand "lookup [something] in [something]" as finding information in.
Understand "lookup [something]" as finding information in.

Understand the command "research" as "lookup".


Finding information in is an action applying to two visible things.

Check finding information in:
	if the second noun is not a manual, say "You aren't likely to find any useful information on [the noun] in that [the second noun]." instead;
	if the second noun is the noun, say "This [the noun] is a Dominion standard issue manual." instead;
	if the noun is not a console, say "I don't believe you'll find any helpful information on [the second noun] in this [the noun]." instead.

Carry out finding information in:
	if the noun is the Life Support Console, now the Life Support Console is understandable.

Report finding information in:
	if the noun is the Life Support Console, say "You spend many silent minutes in the Life Support room looking back and forth between the console and the schematics in [the noun] until you think you are confident enough to mess with the incredibly important ship system without doing more harm.";
	if the noun is the Power Console, say "You find the Power Generator system in the [the noun] but this section of the book is mostly about operations and not about maintenance. Maybe you should try to lookup the error code in the [the noun]."

Rule for supplying a missing second noun while finding information in:
	if the player can touch a manual (called the current manual):
		say "(in the [the current manual])[line break]";
		now the second noun is the current manual;
	otherwise:
		say "You don't have any useful material to lookup in."


Finding code in is an action applying to one number and one visible thing.

Check finding code in:
	if the second noun is not a manual, say "You aren't likely to find any useful information in that." instead;
		
Carry out finding code in:
	if the number understood is 4122, now the Power Console is understandable.
		
Report finding code in:
	if the number understood is 4122:
		say "You look up error code #[the number understood] in the index of [the second noun] and find out it's a simple emergency power mode. But since the levers needed to return it to normal have been damaged you'll need to use a multi-tool to set it straight.";
	otherwise:
		say "There's no entry for [the number understood].";

Rule for supplying a missing second noun while finding code in:
	if the player can touch a manual (called the current manual):
		say "(in the [the current manual])[line break]";
		now the second noun is the current manual;
	otherwise:
		say "You don't have any useful material to lookup in."



	
Understand "fix [something] with [something]" as fixing it with.
Fixing it with is an action applying to two visible things.

Check fixing it with:
	if the second noun is not the multi-tool, say "You probably would do more harm than good trying to fix the [the noun] with that [the second noun]." instead;
	if the second noun is the noun, say "How do you plan on doing that?" instead;
	if the noun is not a console, say "You really don't have the time to stop and fiddle with [the noun] right now." instead;
	if the noun is not broken, say "The [the noun] doesn't appear to be broken, maybe you should put the [the second noun] down before you break it for real." instead;
	if the noun is confusing, say "You don't even know what's wrong with [the noun]. You should probably try and figure out what's wrong with it before you start attacking it with that [the second noun]." instead.

Carry out fixing it with:
	if the noun is the Power Console, now the Power Console is not broken;
	now all doors are unlocked.
	
Report fixing it with:
	if the noun is the Power Console, say "After referring to the manual a few more times you are able to quite easily get the Power Generator to switch off of emergency mode with the help of the [the second noun].";

Rule for supplying a missing second noun while fixing something with:
	say "You probably are going to need more than your bare hands."


Understand "use [something]" as using it.
Understand the command "operate" as "use".
Using it is an action applying to one visible thing.

Check using it:
	if the noun is a radio, say "(Use the command radio 'frequency' instead of use)[line break]" instead;
	if the noun is the multi-tool, say "Don't just go around waving that thing all over the place. You might just blow a hole in the side of the ship." instead;
	if the noun is not a console, say "How are you going to do that?" instead.
	
Carry out using it:
	if the noun is the Life Support Console
	begin;
		if the Life Support Console is confusing
		begin;
			say "you died";
		end if;
	end if;
	
Report using it:
	if the noun is the Life Support Console
	begin;
		if the Life Support Console is understandable, say "You access the Life Support Systems and bring up the control panel. From here you disable the Life Support on a per room basis.[line break](Use the command: toggle life supprt in 'room')[line break]";
	end if;
	

Understand "toggle life support in [any room]" as toggling life support in.
Toggling life support in is an action applying to one thing.
Check toggling life support in:
	if the noun is Mining Vessel, say "You can't disable the life support for a different ship from here." instead;
	if the noun is not a room, say "You can only disable life support for individual rooms on the ship." instead.

To decide whether toggle safe:
	if the noun is Bridge:
		no;
	if the noun is Medical Bay:
		no;
	if the noun is Life Support Systems:
		no;
	yes.

Carry out toggling life support in:
	if toggle safe
	begin;
		if the noun is infested
		begin;
			now the noun is safe;
		end if;
		if the noun is supported
		begin;
			now the noun is not supported;
		otherwise;
			now the noun is supported;
		end if;
	end if;
	
Report toggling life support in:
	if not toggle safe
	begin;
		say "'Error: Human life forms detected in [the noun]. Safety protocols prohibit turning off life support in rooms with live humans.'";
	otherwise;
		if the noun is supported
		begin;
			say "You successfully turn the life support on in [the noun].";
		otherwise;
			say "You successfully turn the life support off in [the noun].";
		end if;
	end if;

Chapter 9 - Everything Else


The Captain is a man. The Captain is in the Bridge. The node is captain-introduction-node.
The Doctor is a man. The Doctor is in the Medical Bay. The node is doctor-firstcontact-node.
